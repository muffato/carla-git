#!/usr/bin/env perl

=head1 Description

Requesting resource requirements is annoyingly repetitive. Let this script help!
Interprets human-readable memory requirements and submits the correct interactive
bsub command.

# run a specific command with 4Gb memory:
ibsub 4gb perl this_is_not_a_script.pl

# run a specific command with 2Gb memory and 2 cores:
ibsub 2gb -n 2 perl this_is_not_a_script.pl

# request basic, empty interactive shell with 100mb memory:
ibsub 100mb

=cut

use strict;
use warnings;

use Getopt::Long qw(:config pass_through);
use Pod::Usage;

my ($mem, $nuc, $queue, $help);
$nuc = 1;
$queue = $ENV{LSB_DEFAULTQUEUE} || "production-rh74";

GetOptions(
	"mem|m=s"   => \$mem,
	"nuc|n=i"   => \$nuc,
	"queue|q=s" => \$queue,
	"help|h"    => \$help,
);

pod2usage({-exitvalue => 0, -verbose => 3}) if $help;

my $mem_h = $mem || shift @ARGV;
$mem_h ||= '1gb';
$mem_h = lc($mem_h);

my ($mult, $unit);
if ( $mem_h =~ /([0-9\.]+)gb/ ) {
	$unit = $1;
	$mult = 1000;
}
elsif ( $mem_h =~ /([0-9]+)mb/ ) {
	$unit = $1;
	$mult = 1;
}
else {
	die ( "Can't figure out memory statement. Please use e.g. 2gb, 4.5gb, 500mb" );
}

my $mb_req = $unit * $mult;

my @cmd;
if (@ARGV) {
	@cmd = @ARGV;
} else {
	push @cmd, $ENV{SHELL};
}

my @bsub = ('bsub', '-q', $queue, "-M$mb_req", qq{-R"select[mem>$mb_req] rusage[mem=$mb_req] span[hosts=1]"}, '-n', $nuc, '-Is');
push @bsub, @cmd;
print "@bsub\n";
exec(@bsub);
